#
# Started from zle or from command line
#

[[ "${ZUI[stdlib_sourced]}" != "1" ]] && source "${ZUI_REPO_DIR}/stdlib.lzui"

# Cleanup $ZUI hash runtime data
-zui_stdlib_cleanup

[[ -o interactivecomments ]] && ZUI[INTERACTIVE_COMMENTS]="1" || ZUI[INTERACTIVE_COMMENTS]="0"
[[ -o promptsubst ]] && ZUI[PROMPT_SUBST]="1" || ZUI[PROMPT_SUBST]="0"

emulate -LR zsh
setopt extendedglob typesetsilent warncreateglobal

[[ "${ZUI[PROMPT_SUBST]}" = "1" ]] && setopt promptsubst

ZUI[app]="zui-test"

##
## Main code
##

# Measure and limit generation time
typeset -F SECONDS=0.0
local -F start_seconds

#
# Actions
#

-zui_external_test_action_1() {
    local -a zip_B
    zip_B=( "$@" )
    local -a zip_A
    zip_A=( "ID:" "Data 1:" "Data 2:" "Data 3:" "Data 4:" )

    print "Hello world!"

    local v1 v2
    for v1 v2 in "${(@)zip_A:^zip_B}"; do
        print "$v1 $v2"
    done
}

-zui_external_anchor_action_2() {
    print "I've jumped even though list has been restarted!"
}

#
# Internal actions
#

# Anchor internal action - data1 is index in time
# of creation of the anchor (it an be changed via
# on-the-fly updates submitted by stdlib function
# -zui_stdlib_submit_list_update
-zui_test_internal_action_1() {
    local id="$1" line="$2" module="$3"

    if [[ "$module" = "2" ]]; then
        zcurses bg "status" "green/black"
        bold="+bold"
    elif [[ "$module" = "3" ]]; then
        zcurses bg "status" "white/blue"
    fi
}

#
# Other callbacks
#

# Optional, unset explicitly if unused
# It generates additional status text, displayed after
# "Current #...", from data of highlighted button
-zui-standard-status-callback() {
    local id data1 data2 data3 data4 text handler
    id="$1"
    data3="$3"
    handler="$7"

    reply=( "My id: $id" " My data: $data3" " My handler:" " $handler" )
}

#
# Generators for module
#

# Generator for module 1
-zui_test_module1_generator() {
    reply=( )
    -zui_stdlib_rc_button "there" "" "/Jump/" "" "" "${ZUI_MAGENTA}there${ZUI_MAGENTA_E}" -zui_external_test_action_1
    # Content
    reply=( "Hello World from ${ZUI_GREEN}module $1${ZUI_GREEN_E}" "Hello ${reply[1]}" "Random number: $RANDOM" )
    # Content count
    reply2=3
    # Non-selectables
    reply3=( 1 )
    # Hops
    reply4=( 1 3 )
}

# Generator for module 2
-zui_test_module2_generator() {
    # Content
    reply=( "Second Module" "I'm not selectable" "Random number: $RANDOM" )
    # Content count
    reply2=3
    # Non-selectables
    reply3=( 2 )
    # Hops
    reply4=( 1 3 )
}

#
# Business logic follows
#

local curcontext=":zle:$WIDGET"
local MATCH; local -i MBEGIN MEND
local -a match mbegin mend

#
# General zstyles
#

#
# Variables
#

# Replies from generation modules
local -a reply reply3 reply4
integer reply2

# Module counter - used to declare parameters
# of given module (they are distinct only in
# counter embedded in them)
local module

# Used to access parameters via name
local var_name

# Used in e.g. for loops as index
integer i

###
### REGENERATION REQUEST LOOP ###
###

local REGENERATE="" REGENERATE_ARG="" REGENERATE_ALWAYS=""

while (( 1 )); do

# Measure generation time
start_seconds=$SECONDS

#
# Common regeneration block
#

if [[ -z "$REGENERATE" || "$REGENERATE" = *,all,* ]]
then

fi

#
# Module 1
#

module=1

if [[ -z "$REGENERATE" || "$REGENERATE" = *,module${module},* || "$REGENERATE" = *,all,* ]]; then
    reply=( ); reply2="0"; reply3=( ); reply4=( )

    # Module 1
    local -a module${module}_output module${module}_nonselectables module${module}_hops
    local -a prev_module${module}_nonselectables prev_module${module}_hops
    integer module${module}_size prev_module${module}_size

    -zui_test_module1_generator $module

    var_name="module${module}_output"
    : ${(PA)var_name::="${reply[@]}"}
    var_name="module${module}_size"
    : ${(PA)var_name::=$reply2}
    var_name="module${module}_nonselectables"
    : ${(PA)var_name::="${reply3[@]}"}
    var_name="module${module}_hops"
    : ${(PA)var_name::="${reply4[@]}"}
fi

#
# Automatic Module 2 & Module 3
#

for (( i=2; i <= 3; i ++ )); do

    module=$i

    if [[ -z "$REGENERATE" || "$REGENERATE" = *,module${module},* || "$REGENERATE" = *,all,* ]]; then
        reply=( ); reply2="0"; reply3=( ); reply4=( )

        local -a module${module}_output module${module}_nonselectables module${module}_hops
        local -a prev_module${module}_nonselectables prev_module${module}_hops
        integer module${module}_size prev_module${module}_size

        -zui_test_module2_generator $module

        var_name="module${module}_output"
        : ${(PA)var_name::="${reply[@]}"}
        var_name="module${module}_size"
        : ${(PA)var_name::=$reply2}
        var_name="module${module}_nonselectables"
        : ${(PA)var_name::="${reply3[@]}"}
        var_name="module${module}_hops"
        : ${(PA)var_name::="${reply4[@]}"}
    fi

done

#
# Store all results into OUTPUT, ZUILIST_NONSELECTABLE_ELEMENTS, ZUILIST_HOP_INDICES
#

typeset -ga ZUILIST_NONSELECTABLE_ELEMENTS ZUILIST_HOP_INDICES
ZUILIST_NONSELECTABLE_ELEMENTS=()
ZUILIST_HOP_INDICES=()

local -a OUTPUT output nonselectables hops
local -a prev_nonselectables prev_hops
local -a anchors
local n
integer LINE_COUNT size

OUTPUT=( )
LINE_COUNT=0
anchors=( )

for (( i = 1; i <= 1000; i ++ )); do
    n="module$i"

    # Check if there are any modules
    var_name="${n}_output"
    [[ "${(P)+var_name}" = "0" ]] && break

    # Get output
    output=( "${(PA@)var_name}" )

    # Get output size
    var_name="${n}_size"
    size="${(P)var_name}"

    # Get nonselectables
    var_name="${n}_nonselectables"
    nonselectables=( "${(PA@)var_name}" )

    # Get hops
    var_name="${n}_hops"
    hops=( "${(PA@)var_name}" )

    # Get previous nonselectables
    var_name="prev_${n}_nonselectables"
    prev_nonselectables=( "${(PA@)var_name}" )

    # Get previous hops
    var_name="prev_${n}_hops"
    prev_hops=( "${(PA@)var_name}" )

    if (( size > 0 )); then
        OUTPUT+=( "" )
        LINE_COUNT+=1
        # + 1 for the anchors at top
        ZUILIST_NONSELECTABLE_ELEMENTS+=( "$(( LINE_COUNT + 1 ))" )
    fi

    # Translate returned indices to global ones
    # The + 1 is for anchors at the top
    nonselectables=( "${nonselectables[@]//(#b)([[:digit:]]##)/$(( ${match[1]} + LINE_COUNT + 1 ))}" )
    hops=( "${hops[@]//(#b)([[:digit:]]##)/$(( ${match[1]} + LINE_COUNT + 1 ))}" )

    # Add translated, current hops and nonselectables
    ZUILIST_NONSELECTABLE_ELEMENTS+=( "${nonselectables[@]}" )
    ZUILIST_HOP_INDICES+=( "${hops[@]}" )

    # Alter current index if size of section was changed
    # - using *_size, prev_*_size parameters
    if [[ "$n" = "module1" && "$REGENERATE" = *,module1,* ]]; then
        integer delta=$(( log_size-prev_log_size ))
        ZUILIST_CURRENT_IDX=$(( ZUILIST_CURRENT_IDX + delta ))
    elif [[ "$n" = "module2" && "$REGENERATE" = *,module2,* ]]; then
        ZUILIST_CURRENT_IDX="4"
        ZUILIST_CURRENT_SEGMENT="1"
    fi

    # Also store the indices into prev_* arrays
    var_name="prev_${n}_nonselectables"
    : "${(PA)var_name::=${nonselectables[@]}}";
    var_name="prev_${n}_hops"
    : "${(PA)var_name::=${hops[@]}}";
    var_name="prev_${n}_size"
    : "${(P)var_name::=${size}}";

    # Create anchors for selected sections
    reply=( )
    if [[ "$i" = "1" ]]; then
        # First argument of -zui_stdlib_anchor is
        # reserved to index to jump to. Second
        # argument, in case of automatically (in
        # a loop) generated modules can be required
        # to be index of the module ($i of this
        # loop)
        [[ -n "${hops[1]}" ]] && -zui_stdlib_anchor "1" "${hops[1]}" "$i" "" "" "[${ZUI_MAGENTA}Module1${ZUI_MAGENTA_E}]" -zui_external_anchor_action_2
    elif [[ "$i" -ge "2" && "$i" -le "3" ]]; then
        [[ -n "${hops[1]}" ]] && -zui_stdlib_anchor "2" "${hops[1]}" "$i" "" "" "[${ZUI_CYAN}Module${i}${ZUI_CYAN_E}]" -zui_test_internal_action_1
    fi
    anchors+=( "${reply[@]}" )

    # Store where in the list module is
    # located, via typical "moduleX_..."
    # variable
    local ${n}_global_index
    var_name="${n}_global_index"
    : ${(P)var_name::=$(( LINE_COUNT + 1 ))}

    OUTPUT+=( "${output[@]}" )
    LINE_COUNT+=size
done

# Reset regeneration variables and prev_current_project
REGENERATE=""
REGENERATE_ARG=""

#
# Generation time
#

local generation_time="$(( SECONDS - start_seconds ))"
ZUI[GENERATION_TIME]="${generation_time[1,4]}"

#
# Invoke the list, handle outcome
#

ZUILIST_HOP_INDICES+=( 1 ) # jump to anchors
local ZUILIST_WRAPPER_BIT=""
zui-usetty-wrapper zui-list-wrapper "${anchors[*]}" "${OUTPUT[@]}" "$generation_time"

if [ -z "$ZUILIST_WRAPPER_BIT" ]; then
    zle && { zle .redisplay; zle .reset-prompt; }

    # Cleanup $ZUI hash runtime data
    -zui_stdlib_cleanup

    return 0
fi

# ID, data1, data2, data3, data4
if -zui_stdlib_decode_hyperlink "$ZUILIST_WRAPPER_BIT"; then
    local id="${reply[1]}" data1="${reply[2]}" data2="${reply[3]}" data3="${reply[4]}" data4="${reply[5]}"

    REGENERATE="$data2"
    REGENERATE_ARG="$data3"
    REGENERATE+="$REGENERATE_ALWAYS"

    integer call_hook=0
    if [[ "$id" = zuieanchor* ]]; then
        ZUILIST_CURRENT_IDX="${ZUI[$id]-$data1}"
        ZUILIST_CURRENT_SEGMENT=1
        id="zuiaction${id#zuieanchor}"
        call_hook=1
    elif [[ "$id" = zuiaction* ]]; then
        call_hook=1
    fi

    if [[ $call_hook -gt 0 && -n "${ZUI[$id]}" ]]; then
        reply[1]="${${${${${reply[1]#zuiiaction}#zuiaction}#zuicheckbox}#zuieanchor}#zuianchor}"
        # Call the handler with all hyper-link data
        "${ZUI[$id]}" "${reply[@]}"
    fi
fi

### REGENERATION REQUEST LOOP ###
done

# Cleanup $ZUI hash runtime data
-zui_stdlib_cleanup

return 0

# vim:ft=zsh
