emulate -L zsh

zmodload zsh/curses

setopt typesetsilent extendedglob

# Compute first to show index - exact page
-zui_list_compute_first_to_show_idx() {
    from_what_idx_list_is_shown=0+((current_idx-1)/page_height)*page_height+1
}

# Conditional, fully robust page-to-show computation
-zui_list_compute_exact_page_if_needed() {
    # Fallback: in case of any problems compute exact page
    if [[ "$from_what_idx_list_is_shown" -lt "1" ||
        "$from_what_idx_list_is_shown" -gt "$last_element" ]]
    then
        from_what_idx_list_is_shown=0+((current_idx-1)/page_height)*page_height+1
    fi
}

# Compute first to show index - page
# scrolled to center to show index
-zui_list_compute_first_to_show_idx_center() {
    from_what_idx_list_is_shown=current_idx-page_height/2
    -zui_list_compute_exact_page_if_needed
}

# Calling this function means passing information
# that we move up in the list (to lower index)
# This means we can move page start as few as 7
# elements up.
-zui_list_compute_first_to_show_idx_up_many() {
    if [[ "$current_idx" -lt "$from_what_idx_list_is_shown" ||
        "$current_idx" -gt "$(( from_what_idx_list_is_shown + page_height -1 ))" ]]
    then
        from_what_idx_list_is_shown=from_what_idx_list_is_shown-7
        -zui_list_compute_exact_page_if_needed
    fi
}

# Like above, but in opposite direction
-zui_list_compute_first_to_show_idx_down_many() {
    if [[ "$current_idx" -lt "$from_what_idx_list_is_shown" ||
        "$current_idx" -gt "$(( from_what_idx_list_is_shown + page_height -1 ))" ]]
    then
        from_what_idx_list_is_shown=from_what_idx_list_is_shown+7
        -zui_list_compute_exact_page_if_needed
    fi
}


typeset -ga reply
reply=( -1 '' )
integer current_idx="$1"
integer current_segment="$2"
integer from_what_idx_list_is_shown="$3"
integer page_height="$4"
integer page_width="$5"
integer last_element="$6"
integer nseg="$7"
integer hscroll="$8"
local key="$9"
integer search="$10"
local buffer="$11"
integer uniq_mode="$12"
integer disable_search="$13"
local list_var_name="$14"

# We need only original element from the list
list_var_name="${list_var_name}[$current_idx]"
local orig_element="${(P)list_var_name}"

#
# Listening for input
#

if [ "$search" = "0" ]; then

case "$key" in
    (UP|k)
        # Are there any elements before the current one?
        [ "$current_idx" -gt 1 ] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_up_many
        }
        [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_up_many
        }
        ;;
    (DOWN|j)
        # Are there any elements after the current one?
        [ "$current_idx" -lt "$last_element" ] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_down_many
        }
        [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_down_many
        }
        ;;
    ($'\C-P')
        [ "$current_idx" -gt 1 ] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_center
        }
        [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_center
        }
        ;;
    ($'\C-N')
        # Are there any elements after the current one?
        [ "$current_idx" -lt "$last_element" ] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_center
        }
        [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_center
        }
        ;;
    (PPAGE|$'\b'|$'\C-?'|BACKSPACE)
        current_idx=current_idx-page_height
        [ "$current_idx" -lt 1 ] && current_idx=1;
        -zui_list_compute_first_to_show_idx
        ;;
    (NPAGE|" ")
        current_idx=current_idx+page_height
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        -zui_list_compute_first_to_show_idx
        ;;
    ($'\C-U')
        current_idx=current_idx-page_height/2
        [ "$current_idx" -lt 1 ] && current_idx=1;
        -zui_list_compute_first_to_show_idx
        ;;
    ($'\C-D')
        current_idx=current_idx+page_height/2
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        -zui_list_compute_first_to_show_idx
        ;;
    (HOME|g)
        current_idx=1
        -zui_list_compute_first_to_show_idx
        ;;
    (END|G)
        current_idx=last_element
        -zui_list_compute_first_to_show_idx
        ;;
    ($'\n'|ENTER)
        # Is that element selectable?
        # Check for this only when there is no search
        if [[ "$ZUILIST_SEARCH_BUFFER" != "" || "$ZUILIST_IS_UNIQ_MODE" -eq 1 ||
            ${ZUILIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} != $current_idx ]]
        then
            # Check if we are at hyperlink. Only then allow selection
            -zui_stdlib_get_segment "$orig_element" $current_segment

            local -a data
            reply=( )
            if -zui_stdlib_decode_hyperlink "$REPLY"; then
                if [[ "${reply[1]}" = zuianchor* || "${reply[1]}" = zuiinternal* ]]; then
                    # This is to run internal action,
                    # i.e. action without leaving list
                    reply=( $current_idx HYPERLINK )
                else
                    # This hyperlink path is through
                    # quitting list and restarting it
                    reply=( $current_idx SELECT )
                fi
            else
                # Selecting non-hyperlink is disallowed
                reply=( -1 '' )
            fi
        fi
        ;;
    (q|Q)
        reply=( -1 QUIT )
        ;;
    (/)
        if [ "$disable_search" = "0" ]; then
            search=1
            -zui_list_cursor_visibility 1
        fi
        ;;
    ($'\C-L')
        reply=( -1 REDRAW )
        ;;
    (\])
        if [[ "${ZUILIST_ENABLED_EVENTS[(r)BR_MOVE_RIGHT]}" = "BR_MOVE_RIGHT" ]]; then
            reply=( "$current_segment" "BR_MOVE_RIGHT" )
        else

        [[ "${(t)ZUILIST_HOP_INDICES}" = "array" || "${(t)ZUILIST_HOP_INDICES}" = "array-local" ]] &&
        [ -z "$ZUILIST_SEARCH_BUFFER" ] && [ "$ZUILIST_IS_UNIQ_MODE" -eq 0 ] &&
        for idx in "${(n)ZUILIST_HOP_INDICES[@]}"; do
            if [ "$idx" -gt "$current_idx" ]; then
                current_idx=$idx
                -zui_list_compute_first_to_show_idx_center
                break
            fi
        done

        fi
        ;;
    (\[)
        if [[ "${ZUILIST_ENABLED_EVENTS[(r)BR_MOVE_LEFT]}" = "BR_MOVE_LEFT" ]]; then
            reply=( "$current_segment" "BR_MOVE_LEFT" )
        else

        [[ "${(t)ZUILIST_HOP_INDICES}" = "array" || "${(t)ZUILIST_HOP_INDICES}" = "array-local" ]] &&
        [ -z "$ZUILIST_SEARCH_BUFFER" ] && [ "$ZUILIST_IS_UNIQ_MODE" -eq 0 ] &&
        for idx in "${(nO)ZUILIST_HOP_INDICES[@]}"; do
            if [ "$idx" -lt "$current_idx" ]; then
                current_idx=$idx
                -zui_list_compute_first_to_show_idx_center
                break
            fi
        done

        fi
        ;;
    ('<'|'{')
        hscroll=hscroll-7
        [ "$hscroll" -lt 0 ] && hscroll=0
        ;;
    ('>'|'}')
        hscroll+=7
        ;;
    (LEFT|'h'|BTAB)
        if [[ "$key" = "BTAB" && ${ZUILIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} = $current_idx ]]; then
            [[ "$current_idx" -gt "1" ]] && {
                current_idx=current_idx-1
                current_segment="100"
                -zui_list_compute_first_to_show_idx_up_many
            }
            [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
                current_idx=current_idx-1
                -zui_list_compute_first_to_show_idx_up_many
            }
        else
            if [ "$current_segment" -gt "$nseg" ]; then
                (( current_segment = nseg - 1 ))
                [ "$current_segment" -le 0 ] && current_segment=1
            else
                if [[ "$key" = "BTAB" && "$current_segment" -eq "1" ]]; then
                    [ "$current_idx" -gt "1" ] && {
                        current_idx=current_idx-1
                        current_segment="100"
                        -zui_list_compute_first_to_show_idx_up_many
                    }
                    [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
                        current_idx=current_idx-1
                        -zui_list_compute_first_to_show_idx_up_many
                    }
                else
                    (( current_segment = current_segment - 1 ))
                fi
            fi
        fi
        ;;
    (RIGHT|'l'|$'\t')
        if [[ "$key" = $'\t' && ${ZUILIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} = $current_idx ]]; then
            [ "$current_idx" -lt "$last_element" ] && {
                current_idx=current_idx+1
                current_segment=1
                -zui_list_compute_first_to_show_idx_down_many
            }
            [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
                current_idx=current_idx+1
                -zui_list_compute_first_to_show_idx_down_many
            }
        else
            (( current_segment = current_segment + 1 ))
            if [ "$key" = $'\t' ]; then
                if [[ "$current_segment" -gt "$nseg" ]]; then
                    [ "$current_idx" -lt "$last_element" ] && {
                        current_idx=current_idx+1
                        current_segment="1"
                        -zui_list_compute_first_to_show_idx_down_many
                    }
                    [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
                        current_idx=current_idx+1
                        -zui_list_compute_first_to_show_idx_down_many
                    }
                fi
            fi
            [ "$current_segment" -gt "$nseg" ] && current_segment="$nseg"
        fi
        ;;
    (F1|F2|F3|F4)
        reply=( -1 "$key" )
        ;;
    ($'\E')
        buffer=""
        ;;
    (o|$'\C-O')
        uniq_mode=1-uniq_mode
        ;;
    (*)
        ;;
esac

else

case "$key" in
    ($'\n'|ENTER)
        search=0
        -zui_list_cursor_visibility 0
        ;;
    ($'\C-L')
        reply=( -1 REDRAW )
        ;;

    #
    # Slightly limited navigation
    #

    (UP)
        [ "$current_idx" -gt 1 ] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_up_many
        }
        [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_up_many
        }
        ;;
    (DOWN)
        [ "$current_idx" -lt "$last_element" ] && {
            current_idx=current_idx+1;
            -zui_list_compute_first_to_show_idx_down_many
        }
        [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_down_many
        }
        ;;
    ($'\C-P')
        [ "$current_idx" -gt 1 ] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_center
        }
        [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
            current_idx=current_idx-1
            -zui_list_compute_first_to_show_idx_center
        }
        ;;
    ($'\C-N')
        # Are there any elements after the current one?
        [ "$current_idx" -lt "$last_element" ] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_center
        }
        [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
            current_idx=current_idx+1
            -zui_list_compute_first_to_show_idx_center
        }
        ;;
    (PPAGE)
        current_idx=current_idx-page_height
        [ "$current_idx" -lt 1 ] && current_idx=1;
        -zui_list_compute_first_to_show_idx
        ;;
    (NPAGE)
        current_idx=current_idx+page_height
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        -zui_list_compute_first_to_show_idx
        ;;
    ($'\C-U')
        current_idx=current_idx-page_height/2
        [ "$current_idx" -lt 1 ] && current_idx=1;
        -zui_list_compute_first_to_show_idx
        ;;
    ($'\C-D')
        current_idx=current_idx+page_height/2
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        -zui_list_compute_first_to_show_idx
        ;;
    (HOME)
        current_idx=1
        -zui_list_compute_first_to_show_idx
        ;;
    (END)
        current_idx=last_element
        -zui_list_compute_first_to_show_idx
        ;;
    (LEFT|BTAB)
        if [[ "$key" = "BTAB" && ${ZUILIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} = $current_idx ]]; then
            [[ "$current_idx" -gt "1" ]] && {
                current_idx=current_idx-1
                current_segment="100"
                -zui_list_compute_first_to_show_idx_up_many
            }
            [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
                current_idx=current_idx-1
                -zui_list_compute_first_to_show_idx_up_many
            }
        else
            if [ "$current_segment" -gt "$nseg" ]; then
                (( current_segment = nseg - 1 ))
                [ "$current_segment" -le 0 ] && current_segment=1
            else
                if [[ "$key" = BTAB && "$current_segment" -eq "1" ]]; then
                    [ "$current_idx" -gt "1" ] && {
                        current_idx=current_idx-1
                        current_segment="100"
                        -zui_list_compute_first_to_show_idx_up_many
                    }
                    [[ -z "$orig_element" && "$current_idx" -gt 1 ]] && {
                        current_idx=current_idx-1
                        -zui_list_compute_first_to_show_idx_up_many
                    }
                else
                    (( current_segment = current_segment - 1 ))
                fi
            fi
        fi
        ;;
    (RIGHT|$'\t')
        if [[ "$key" = $'\t' && ${ZUILIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} = $current_idx ]]; then
            [ "$current_idx" -lt "$last_element" ] && {
                current_idx=current_idx+1
                current_segment=1
                -zui_list_compute_first_to_show_idx_down_many
            }
            [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
                current_idx=current_idx+1
                -zui_list_compute_first_to_show_idx_down_many
            }
        else
            (( current_segment = current_segment + 1 ))
            if [ "$key" = $'\t' ]; then
                if [[ "$current_segment" -gt "$nseg" ]]; then
                    [ "$current_idx" -lt "$last_element" ] && {
                        current_idx=current_idx+1
                        current_segment="1"
                        -zui_list_compute_first_to_show_idx_down_many
                    }
                    [[ -z "$orig_element" && "$current_idx" -lt "$last_element" ]] && {
                        current_idx=current_idx+1
                        -zui_list_compute_first_to_show_idx_down_many
                    }
                fi
            fi
            [ "$current_segment" -gt "$nseg" ] && current_segment="$nseg"
        fi
        ;;
    (F1|F2|F3|F4)
        reply=( -1 "$key" )
        ;;
    (F4|F5|F6|F7|F8|F9|F10)
        # ignore
        ;;

    #
    # The input
    #

    ($'\b'|$'\C-?'|BACKSPACE)
        buffer="${buffer%?}"
        ;;
    ($'\C-W')
        [ "$buffer" = "${buffer% *}" ] && buffer="" || buffer="${buffer% *}"
        ;;
    ($'\C-K')
        buffer=""
        ;;
    ($'\E')
        buffer=""
        search=0
        -zui_list_cursor_visibility 0
        ;;
    ($'\C-O')
        uniq_mode=1-uniq_mode
        ;;
    (*)
        if [[ $#key == 1 && $((#key)) -lt 31 ]]; then
            # ignore all other control keys
        else
            buffer+="$key"
        fi
        ;;
esac

fi

reply[3]="$current_idx"
reply[4]="$current_segment"
reply[5]="$from_what_idx_list_is_shown"
reply[6]="$hscroll"
reply[7]="$search"
reply[8]="$buffer"
reply[9]="$uniq_mode"

# vim: set filetype=zsh:
